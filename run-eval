#!/usr/bin/env perl

use strict;
use warnings;
use Getopt::Std;

my %opts = (g=>37, l=>2, L=>50, q=>0);
getopts('g:l:L:o:HFucfB:q:', \%opts);

# check path
my $exepath = $0 =~/^\S+\/[^\/\s]+/? $0 : &which($0);
my $root = $0 =~/^(\S+)\/[^\/\s]+/? $1 : undef;
$root = $exepath =~/^(\S+)\/[^\/\s]+/? $1 : undef if !defined($root);
die "ERROR: failed to locate the root directory\n" if !defined($root);

die("Usage: run [options] <test.vcf>
Options:
  -g STR      genome build: 37, 37d5, 38 or 38DH [37]
  -o STR      output prefix [auto]
  -f          evaluate in functional regions only [all]
  -H          *include* errors *contained* in ploy-A longer than 10bp
  -l INT      min INDEL length [$opts{l}]
  -L INT      max INDEL length [$opts{L}]
  -B FILE     exclude errors *contained* in BED FILE [null]
  -c          exclude errors *contained* in low-complexity regions
  -u          exclude errors *contained* in the um75 mask (37d5 only)
  -F          ignore the FILTER field
  -q INT      min QUAL [$opts{q}]
") if @ARGV < 1;

# infer prefix
my $prefix;
if (defined $opts{o}) {
	$prefix = $opts{o};
} elsif ($ARGV[0] =~ /\.vcf(\.gz?)$/) {
	$prefix = $ARGV[0];
	$prefix =~ s/\.vcf(\.gz?)$//;
}
die "ERROR: failed to infer the prefix for output. Please specify -o.\n" unless defined($prefix);

# figure out the genome build
my %valid_g = ('37m'=>1, '38'=>1, '37d5'=>1, '38DH'=>1);
$opts{g} =~ s/^hs//;
$opts{g} = '37' if $opts{g} eq 'hg19';
$opts{g} = '38' if $opts{g} eq 'hg38';
$opts{g} = '37m' if $opts{g} eq '37';
die "ERROR: failed to infer the genome build from hint '$opts{g}'.\n" unless $valid_g{$opts{g}};

# print evaluation command line
die "ERROR: option '-u' is only applicable to -g37 or -g37d5.\n" if defined($opts{u}) && $opts{g} ne '37m' && $opts{g} ne '37d5';
my $opts_hapdip = '';
$opts_hapdip .= " -B $root/ployA10-$opts{g}.bed.gz" unless defined($opts{H});
$opts_hapdip .= " -B $root/sdust30-$opts{g}.bed.gz" if defined($opts{c}); 
$opts_hapdip .= " -B $root/um75-hs37d5.bed.gz" if defined($opts{u});
$opts_hapdip .= " -q$opts{q}" if defined($opts{q}) && $opts{q} > 0;
$opts_hapdip .= " -B $opts{B}" if defined($opts{B}) && -f $opts{B};
$opts_hapdip .= " -F" if defined($opts{F});
$opts_hapdip =~ s/^\s+//;
my $conf_bed = defined($opts{f})? "$root/hybrid-func.m$opts{g}.bed.gz" : "$root/hybrid.m$opts{g}.bed.gz";
die "ERROR: failed to locate the confident region BED.\n" unless -f $conf_bed;
my $cmd = "$root/k8 $root/hapdip.js distEval -s $prefix.summary -l $opts{l} -L $opts{L} $opts_hapdip -b $conf_bed $root/hybrid.m$opts{g}.vcf.gz $ARGV[0] | $root/htsbox bgzip > $prefix.err.bed.gz";
print "$cmd\n";

sub which {
	my $file = shift;
	my $path = (@_)? shift : $ENV{PATH};
	return if (!defined($path));
	foreach my $x (split(":", $path)) {
		$x =~ s/\/$//;
		return "$x/$file" if (-x "$x/$file");
	}
	return;
}
