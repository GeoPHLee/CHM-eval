#!/usr/bin/env perl

use strict;
use warnings;
use Getopt::Std;

my %opts = (g=>37, l=>2, L=>100);
getopts('g:l:L:o:HucfB:', \%opts);

# check path
my $exepath = $0 =~/^\S+\/[^\/\s]+/? $0 : &which($0);
my $root = $0 =~/^(\S+)\/[^\/\s]+/? $1 : undef;
$root = $exepath =~/^(\S+)\/[^\/\s]+/? $1 : undef if !defined($root);
die "ERROR: failed to locate the root directory\n" if !defined($root);

die("Usage: CHM-eval/run [options] <test.vcf>
Options:
  -g STR      genome build: 37, 37d5, 38 or 38DH [37]
  -o STR      output prefix [auto]
  -l INT      min INDEL length [$opts{l}]
  -L INT      max INDEL length [$opts{L}]
  -f          evaluate in functional regions only (37 and 37d5 only) [all]
  -B FILE     exclude errors overlapping BED FILE (overriding -c and -u) [null]
  -c          exclude errors overlapping low-complexity regions (overriding -u) [null]
  -u          exclude errors overlapping the um75 mask (37 only) [null]
  -H          include errors homopolymer regions [exclude]
") if @ARGV < 1;

# infer prefix
my $prefix;
if (defined $opts{o}) {
	$prefix = $opts{o};
} elsif ($ARGV[0] =~ /\.vcf(\.gz?)$/) {
	$prefix = $ARGV[0];
	$prefix =~ s/\.vcf(\.gz?)$//;
}
die "ERROR: failed to infer the prefix for output. Please specify -o.\n" unless defined($prefix);

# figure out the genome build
my %valid_g = ('37m'=>1, '38'=>1, '37d5'=>1, '38DH'=>1);
$opts{g} =~ s/^hs//;
$opts{g} = '37' if $opts{g} eq 'hg19';
$opts{g} = '38' if $opts{g} eq 'hg38';
$opts{g} = '37m' if $opts{g} eq '37';
die "ERROR: failed to infer the genome build from hint '$opts{g}'.\n" unless $valid_g{$opts{g}};

# print evaluation command line
die "ERROR: option '-u' is only applicable to -g37 or -g37d5.\n" if defined($opts{u}) && $opts{g} ne '37m' && $opts{g} ne '37d5';
my $opt_p = "-p $root/hs$opts{g}.hrun5.gz" unless defined($opts{H});
my $opt_B = '';
if (defined($opts{B}) && -f $opts{B}) {
	$opt_B = "-B $opts{B}";
} elsif (defined($opts{c})) {
	$opt_B = "-B $root/hs$opts{g}.sdust30.bed.gz";
} elsif (defined($opts{u})) {
	$opt_B = "-B $root/um75-hs37d5.bed.gz";
}
my $conf_bed = defined($opts{f})? "$root/hybrid-func.m$opts{g}.bed.gz" : "$root/hybrid.m$opts{g}.bed.gz";
die "ERROR: failed to locate the confident region BED.\n" unless -f $conf_bed;
my $cmd = "$root/k8 $root/hapdip.js distEval -s $prefix.summary -l $opts{l} -L $opts{L} $opt_p $opt_B -b $conf_bed $root/hybrid.m$opts{g}.vcf.gz $ARGV[0] | $root/htsbox bgzip > $prefix.err.bed.gz";
print "$cmd\n";

sub which {
	my $file = shift;
	my $path = (@_)? shift : $ENV{PATH};
	return if (!defined($path));
	foreach my $x (split(":", $path)) {
		$x =~ s/\/$//;
		return "$x/$file" if (-x "$x/$file");
	}
	return;
}
